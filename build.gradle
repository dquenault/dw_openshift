apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'fatjar'

// Use Java 7 by default
sourceCompatibility = '1.7'
targetCompatibility = '1.7'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

// The main class of the application
mainClassName = 'com.tearsofaunicorn.diy.HelloWorldService'


repositories {
   mavenCentral()
}

dependencies {
   compile fileTree(dir: 'libs', include: '*.jar')
}

buildscript {
    dependencies {
        classpath fileTree(dir: 'libs',
                includes: [
                        'gradle-fatjar-plugin-0.2-rc1.jar'
                ]
        )
    }
}


fatJar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
    exclude 'META-INF/.DSA', 'META-INF/.RSA', 'META-INF/*.SF'
}

task zip(type: Zip) {
    baseName = 'product-service'
    from fatJar.outputs.files
    from ('.'){
        include "*.yml"
    }
}

// Configure the run task to start the Dropwizard service
// Assumes that yml file is in base directory of repo
run {
    def environment = System.getProperty('environment')
    if (!environment){
        environment = 'local'
    }
    args 'server', environment + '.yml'
}

